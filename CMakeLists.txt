cmake_minimum_required(VERSION 2.8)
project (Functors)

# Find and import OpenCL.
option(FNC_ENABLE_OPENCL
  "Enable/disable OpenCL (required for any real execution)."
  OFF)

if (FNC_ENABLE_OPENCL)
  set (CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/FindOpenCL")
  find_package(OpenCL REQUIRED)
  include_directories(${OPENCL_INCLUDE_DIRS})
  if (NOT OPENCL_HAS_CPP_BINDINGS)
    include_directories("${CMAKE_SOURCE_DIR}/OpenCLCpp")
  endif()
endif()

# List of Boost features used:
# * Smart Ptr
# * xpressive
# * program options
find_package(Boost REQUIRED
  COMPONENTS program_options # program_options is only needed for the testing
                             # executables
  )

include_directories(${Boost_INCLUDE_DIRS}
  ${CMAKE_CURRENT_BINARY_DIR}

  )

add_executable(fncGenerateHeaderFromFile
  fncGenerateHeaderFromFile.cxx)
target_link_libraries(fncGenerateHeaderFromFile
  ${Boost_LIBRARIES})

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/fncOptions.h.in
  ${CMAKE_CURRENT_BINARY_DIR}/fncOptions.h
  @ONLY)

function (encode_to_headers out_variable)
  set (all_headers)
  foreach(file ${ARGN})
    set(src ${CMAKE_CURRENT_SOURCE_DIR}/${file})
    get_filename_component(filename ${file} NAME)
    set(res ${CMAKE_CURRENT_BINARY_DIR}/${filename}.h)
    list(APPEND all_headers ${res})
    add_custom_command(
      OUTPUT ${res}
      DEPENDS ${src} fncGenerateHeaderFromFile
      COMMAND fncGenerateHeaderFromFile
      ARGS  --input-file ${src} --output-file ${res}
      )
  endforeach()
  set (${out_variable} ${all_headers} PARENT_SCOPE)
endfunction()

set (functor_modules
  Functors/PassThrough.cl
)

set (core_kernels
  Resources/CoreKernel.cl
  Resources/CoreMapField.cl
  Resources/CorePointIterator.cl
  Resources/CoreImageData.cl
)

set (generated_headers_core)
encode_to_headers(generated_headers_core
  ${core_kernels}
  )
set (generated_headers)
encode_to_headers(generated_headers
  ${functor_modules}
  )
#message ("Headers: ${generated_headers}")

# fncFramework is the core library
add_library(fncFramework SHARED
  fncAutoGeneratedModule.cxx
  fncAutoGeneratedModule.h
  fncDataTraits.h
  fncExecutive.cxx
  fncExecutive.h
  fncImageData.cxx
  fncImageData.h
  fncModule.cxx
  fncModule.h
  fncObject.cxx
  fncObject.h
  fncPort.cxx
  fncPort.h
  ${generated_headers_core}
)

target_link_libraries(fncFramework
  ${Boost_LIBRARIES}
  )

if(FNC_ENABLE_OPENCL)
  target_link_libraries(fncFramework ${OPENCL_LIBRARIES})
endif()

foreach (file ${generated_headers_core})
  set_source_files_properties(fncObject.h
    PROPERTIES OBJECT_DEPENDS ${file})
endforeach()

# Add executables, mostly testing samples
add_executable(fncTest
  fncTest.cxx
  ${generated_headers}
  )

foreach (file ${generated_headers})
  set_source_files_properties(fncTest.cxx
    PROPERTIES OBJECT_DEPENDS ${file})
endforeach()

target_link_libraries(fncTest
  fncFramework
  )
